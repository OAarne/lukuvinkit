plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.1'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'


repositories {
    jcenter()
}

dependencies {
    compile 'org.json:json:20180813'
    compile 'org.jline:jline:3.9.0'
    testCompile 'junit:junit:4.12'
    testCompile 'io.cucumber:cucumber-java:4.2.0'
    testCompile 'io.cucumber:cucumber-junit:4.2.0'
}

tasks.withType(JavaCompile) {
    options.compilerArgs += '-Xlint:all'
    options.compilerArgs += '-Werror'
}

mainClassName = 'lukuvinkit.Lukuvinkit'
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

run {
    standardInput = System.in
    enableAssertions = true
}

test {
    /* Tällä saadaan gradle test näyttämään cucumberin tulostamat ilmoitukset. */
    testLogging.showStandardStreams = true
    enableAssertions = true
}

jacocoTestReport {
    reports {
        /* Xml-raportti tarvitaan codecovia varten. */
        xml.enabled true
        html.enabled true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['lukuvinkit/ui**', 'lukuvinkit/Lukuvinkit**'])
        })
    }

}

task fatJar(type: Jar) {
    manifest {
        attributes "Main-Class": mainClassName
    }
    baseName = project.name + "-fat"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
